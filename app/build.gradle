apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply from: '../secrets.gradle'

def fabricApiKey = loadSecret("FABRIC_API_KEY", "fabricApiKey")
def googleMapsDebugKey = loadSecret("GOOGLE_MAPS_DEBUG_KEY", "googleMapsDebugKey")
def googleMapsReleaseKey = loadSecret("GOOGLE_MAPS_RELEASE_KEY", "googleMapsReleaseKey")

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "pl.temomuko.autostoprace"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 16
        versionName "1.1.5"
        manifestPlaceholders = [crashlyticsApiKey: fabricApiKey]
    }

    buildTypes {
        debug {
            resValue 'string', 'GOOGLE_MAPS_KEY', googleMapsDebugKey
        }
        release {
            resValue 'string', 'GOOGLE_MAPS_KEY', googleMapsReleaseKey
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    versions = [
            support: "27.0.2",
            okhttp: "3.9.1",
            retrofit: "2.3.0",
            leakcanary: "1.5.1",
            butterknife: "8.0.1"
    ]
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:1.10.9"

    implementation "com.google.dagger:dagger:2.14.1"
    annotationProcessor "com.google.dagger:dagger-compiler:2.14.1"

    implementation "com.android.support:design:$versions.support"
    implementation "com.android.support:cardview-v7:$versions.support"

    implementation "me.zhanghai.android.materialprogressbar:library:1.4.2"
    implementation "org.greenrobot:eventbus:3.0.0"

    implementation "com.jakewharton:butterknife:$versions.butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterknife"

    implementation "com.afollestad.material-dialogs:core:0.9.6.0"
    implementation "com.davemorrissey.labs:subsampling-scale-image-view:3.6.0"

    implementation "io.reactivex:rxjava:1.2.9"
    implementation "io.reactivex:rxandroid:1.2.0"
    implementation "com.jakewharton.rxbinding:rxbinding:0.4.0"
    implementation "com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0"

    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"

    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakcanary"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakcanary"
    implementation "com.google.code.gson:gson:2.8.2"
    implementation "com.squareup.sqlbrite:sqlbrite:0.6.3"
    implementation "net.sf.opencsv:opencsv:2.3"
    implementation "com.github.bumptech.glide:glide:3.8.0"

    implementation "com.google.android.gms:play-services-location:11.8.0"
    implementation "com.google.android.gms:play-services-maps:11.8.0"
    implementation "com.google.maps.android:android-maps-utils:0.5"
    implementation "com.crashlytics.sdk.android:crashlytics:2.8.0"

    implementation "com.vanniktech:onactivityresult:0.6.0"
    annotationProcessor "com.vanniktech:onactivityresult-compiler:0.6.0"

    implementation "com.theartofdev.edmodo:android-image-cropper:2.3.1"
}
